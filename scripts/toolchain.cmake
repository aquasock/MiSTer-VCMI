# Cross toolchain for the MiSTer (Cortex-A9 + NEON, armhf, Linux).
set(CMAKE_SYSTEM_NAME Linux)
set(CMAKE_SYSTEM_PROCESSOR arm)

set(CMAKE_C_COMPILER   arm-linux-gnueabihf-gcc)
set(CMAKE_CXX_COMPILER arm-linux-gnueabihf-g++)

set(_arch "-mcpu=cortex-a9 -mfpu=neon -mfloat-abi=hard -fPIC")
set(CMAKE_C_FLAGS_INIT   "${_arch}")
set(CMAKE_CXX_FLAGS_INIT "${_arch}")

set(CMAKE_FIND_ROOT_PATH "$ENV{VCMI_PREFIX}" /usr/arm-linux-gnueabihf)
set(CMAKE_PREFIX_PATH    "$ENV{VCMI_PREFIX}")
set(CMAKE_FIND_ROOT_PATH_MODE_PROGRAM NEVER)
set(CMAKE_FIND_ROOT_PATH_MODE_LIBRARY ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_INCLUDE ONLY)
set(CMAKE_FIND_ROOT_PATH_MODE_PACKAGE ONLY)

# Lets try_run() checks execute under qemu on the build machine.
set(CMAKE_CROSSCOMPILING_EMULATOR qemu-arm -L /usr/arm-linux-gnueabihf)
